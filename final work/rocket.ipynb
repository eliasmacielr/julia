{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, Ipopt"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\\title{Lanzamiento de un Cohete}\n",
    "\n",
    "Un cohete debe ser lanzado de forma vertical y debe, a partir \n",
    "del impulso, usado como control, alcanzar la mayor altura \n",
    "posible.\n",
    "\n",
    "Este es un problema de optimización dinámica.\n",
    "\n",
    "Para este problema tomamos:\n",
    "\n",
    "- $t$ : tiempo;\n",
    "- $m(t)$ : masa del cohete;\n",
    "- $v(t)$ : velocidad del cohete;\n",
    "- $I(t)$ : impulso del cohete.\n",
    "\n",
    "Las ecuaciones del movimiento del cohete son\n",
    "\\begin{equation} \\label{eq:h_dot}\n",
    "h' = v,\n",
    "\\end{equation}\n",
    "\n",
    "\\begin{equation} \\label{eq:m_dot}\n",
    "m' = -{v_0}^{-1} I(t),\n",
    "\\end{equation}\n",
    "\n",
    "\\begin{equation} \\label{eq:v_dot}\n",
    "v' = m^{-1} [I(t) - D(v, h)] - G(h),\n",
    "\\end{equation}\n",
    "donde $h$ es la altura a la que se encuentra el cohete, $v$ es \n",
    "su velocidad vertical, $v_0$ es una constante que mide el \n",
    "impulso del combustible del cohete, $D(v, h)$ es la resistencia\n",
    "del aire y $G(h)$ es la fuerza de la gravedad, que se definen \n",
    "como siendo\n",
    "\\begin{equation}\n",
    "G(h) = c_1 h^{-2}\n",
    "\\end{equation}\n",
    "y\n",
    "\\begin{equation}\n",
    "D(v, h) = c_2 v^2 e^{-\\beta h},\n",
    "\\end{equation}\n",
    "donde $c_1$ es una constante correspondiente a la fuerza \n",
    "gravitacional en la superficie de la tierra, $c_2$ y $\\beta$ son\n",
    "constantes."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 41,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Solving...\n",
      "This is Ipopt version 3.12.10, running with linear solver mumps.\n",
      "NOTE: Other linear solvers might be more efficient (see Ipopt documentation).\n",
      "\n",
      "Number of nonzeros in equality constraint Jacobian...:    16804\n",
      "Number of nonzeros in inequality constraint Jacobian.:        0\n",
      "Number of nonzeros in Lagrangian Hessian.............:    39200\n",
      "\n",
      "Total number of variables............................:     3205\n",
      "                     variables with only lower bounds:     1603\n",
      "                variables with lower and upper bounds:     1602\n",
      "                     variables with only upper bounds:        0\n",
      "Total number of equality constraints.................:     2404\n",
      "Total number of inequality constraints...............:        0\n",
      "        inequality constraints with only lower bounds:        0\n",
      "   inequality constraints with lower and upper bounds:        0\n",
      "        inequality constraints with only upper bounds:        0\n",
      "\n",
      "iter    objective    inf_pr   inf_du lg(mu)  ||d||  lg(rg) alpha_du alpha_pr  ls\n",
      "   0 -1.0100000e+00 3.50e-02 1.83e+00  -1.0 0.00e+00    -  0.00e+00 0.00e+00   0\n",
      "   1 -1.0476357e+00 3.10e-03 7.60e+03  -1.0 2.38e-01   2.0 9.18e-01 9.67e-01f  1\n",
      "   2 -1.0330218e+00 1.03e-03 8.61e+04  -1.0 2.31e+00    -  3.71e-01 7.53e-01h  1\n",
      "   3 -1.0108102e+00 3.89e-04 1.79e+05  -1.0 1.27e+00    -  1.00e+00 6.40e-01h  1\n",
      "   4 -1.0058561e+00 1.58e-04 2.97e+05  -1.0 6.84e-01    -  1.00e+00 9.69e-01h  1\n",
      "   5 -1.0078548e+00 9.05e-05 2.16e+05  -1.0 8.08e-01    -  1.00e+00 6.33e-01f  1\n",
      "   6 -1.0079653e+00 1.75e-05 4.11e+04  -1.0 4.89e-01    -  1.00e+00 1.00e+00f  1\n",
      "   7 -1.0078191e+00 1.38e-06 1.02e+02  -1.0 1.70e-01    -  1.00e+00 1.00e+00f  1\n",
      "   8 -1.0078115e+00 3.32e-10 2.15e+02  -1.7 3.11e-03    -  1.00e+00 1.00e+00h  1\n",
      "   9 -1.0078157e+00 2.53e-10 4.46e-01  -1.7 1.20e-02    -  1.00e+00 1.00e+00h  1\n",
      "iter    objective    inf_pr   inf_du lg(mu)  ||d||  lg(rg) alpha_du alpha_pr  ls\n",
      "  10 -1.0078162e+00 6.07e-12 1.15e+01  -3.8 4.17e-04    -  1.00e+00 1.00e+00h  1\n",
      "  11 -1.0078877e+00 1.06e-07 5.24e-03  -3.8 5.52e-02    -  1.00e+00 1.00e+00f  1\n",
      "  12 -1.0078881e+00 2.27e-13 1.65e-06  -3.8 3.09e-04    -  1.00e+00 1.00e+00h  1\n",
      "  13 -1.0079601e+00 1.06e-07 2.95e+02  -5.7 5.49e-02    -  9.80e-01 1.00e+00f  1\n",
      "  14 -1.0095526e+00 8.48e-05 1.22e+00  -5.7 2.23e+00    -  1.00e+00 9.65e-01h  1\n",
      "  15 -1.0112897e+00 5.88e-05 2.95e+00  -5.7 1.78e+00    -  1.00e+00 9.90e-01f  1\n",
      "  16 -1.0111071e+00 2.22e-06 2.07e-02  -5.7 7.35e-01    -  1.00e+00 1.00e+00f  1\n",
      "  17 -1.0111190e+00 6.83e-09 6.56e-05  -5.7 5.17e-02    -  1.00e+00 1.00e+00h  1\n",
      "  18 -1.0111191e+00 4.13e-14 1.20e-09  -5.7 3.56e-04    -  1.00e+00 1.00e+00h  1\n",
      "  19 -1.0122683e+00 2.30e-05 2.99e+01  -8.6 8.98e-01    -  7.15e-01 8.77e-01f  1\n",
      "iter    objective    inf_pr   inf_du lg(mu)  ||d||  lg(rg) alpha_du alpha_pr  ls\n",
      "  20 -1.0127029e+00 1.51e-05 8.14e+00  -8.6 8.55e-01    -  7.50e-01 7.80e-01h  1\n",
      "  21 -1.0128032e+00 1.01e-05 3.13e+00  -8.6 1.26e+00    -  6.59e-01 7.31e-01h  1\n",
      "  22 -1.0128269e+00 5.32e-06 1.22e+00  -8.6 1.40e+00    -  6.46e-01 7.33e-01h  1\n",
      "  23 -1.0128326e+00 2.58e-06 3.96e-01  -8.6 1.38e+00    -  6.98e-01 7.71e-01f  1\n",
      "  24 -1.0128339e+00 1.13e-06 1.87e-02  -8.6 1.24e+00    -  9.40e-01 8.79e-01f  1\n",
      "  25 -1.0128341e+00 2.77e-07 5.12e-05  -8.6 9.73e-01    -  1.00e+00 1.00e+00f  1\n",
      "  26 -1.0128341e+00 3.97e-09 1.13e-06  -8.6 2.34e-01    -  1.00e+00 1.00e+00h  1\n",
      "  27 -1.0128341e+00 1.03e-10 7.14e-09  -8.6 3.13e-02    -  1.00e+00 1.00e+00h  1\n",
      "\n",
      "Number of Iterations....: 27\n",
      "\n",
      "                                   (scaled)                 (unscaled)\n",
      "Objective...............:  -1.0128340615485474e+00   -1.0128340615485474e+00\n",
      "Dual infeasibility......:   7.1352764892224730e-09    7.1352764892224730e-09\n",
      "Constraint violation....:   1.0326103860869296e-10    1.0326103860869296e-10\n",
      "Complementarity.........:   2.5081820874398648e-09    2.5081820874398648e-09\n",
      "Overall NLP error.......:   7.1352764892224730e-09    7.1352764892224730e-09\n",
      "\n",
      "\n",
      "Number of objective function evaluations             = 28\n",
      "Number of objective gradient evaluations             = 28\n",
      "Number of equality constraint evaluations            = 28\n",
      "Number of inequality constraint evaluations          = 0\n",
      "Number of equality constraint Jacobian evaluations   = 28\n",
      "Number of inequality constraint Jacobian evaluations = 0\n",
      "Number of Lagrangian Hessian evaluations             = 27\n",
      "Total CPU secs in IPOPT (w/o function evaluations)   =      0.269\n",
      "Total CPU secs in NLP function evaluations           =      0.332\n",
      "\n",
      "EXIT: Optimal Solution Found.\n",
      "Max height: 1.0128340615485474\n"
     ]
    }
   ],
   "source": [
    "mod = Model(with_optimizer(Ipopt.Optimizer))\n",
    "\n",
    "# Constants\n",
    "# Note that all parameters in the model have been normalized\n",
    "# to be dimensionless. See the COPS3 paper for more info.\n",
    "h_0 = 1    # Initial height\n",
    "v_0 = 0    # Initial velocity\n",
    "m_0 = 1    # Initial mass\n",
    "g_0 = 1    # Gravity at the surface\n",
    "\n",
    "# Parameters\n",
    "T_c = 3.5  # Used for thrust\n",
    "h_c = 500  # Used for drag\n",
    "v_c = 620  # Used for drag\n",
    "m_c = 0.6  # Fraction of initial mass left at end\n",
    "\n",
    "# Derived parameters\n",
    "c     = 0.5*sqrt(g_0*h_0)  # Thrust-to-fuel mass\n",
    "m_f   = m_c*m_0            # Final mass\n",
    "D_c   = 0.5*v_c*m_0/g_0    # Drag scaling\n",
    "T_max = T_c*g_0*m_0        # Maximum thrust\n",
    "\n",
    "n = 800   # Time steps\n",
    "@variable(mod, Δt ≥ 0, start = 1/n)   # Time step\n",
    "#@expression(t_f, Δt*n)               # Time of flight\n",
    "\n",
    "# State variables\n",
    "@variable(mod, v[0:n] ≥ 0)            # Velocity\n",
    "@variable(mod, h[0:n] ≥ h_0)          # Height\n",
    "@variable(mod, m_f ≤ m[0:n] ≤ m_0)    # Mass\n",
    "\n",
    "# Control: thrust\n",
    "@variable(mod, 0 ≤ T[0:n] ≤ T_max)\n",
    "\n",
    "# Initial conditions\n",
    "@NLconstraint(mod, v[0] == v_0)\n",
    "@NLconstraint(mod, h[0] == h_0)\n",
    "@NLconstraint(mod, m[0] == m_0)\n",
    "@NLconstraint(mod, m[n] == m_f)\n",
    "\n",
    "\n",
    "# @variable(mod, x[i = 1:3])\n",
    "# @expression(mod, expr[i = 1:3], i * sum(x[j] for j in i:3))\n",
    "\n",
    "# Forces\n",
    "# Drag(h,v) = Dc v^2 exp( -hc * (h - h0) / h0 )\n",
    "@NLexpression(mod, drag[j=0:n], D_c*(v[j]^2)*exp(-h_c*(h[j]-h_0)/h_0))\n",
    "# Grav(h)   = go * (h0 / h)^2\n",
    "@NLexpression(mod, grav[j=0:n], g_0*(h_0/h[j])^2)\n",
    "\n",
    "# Dynamics\n",
    "for j in 1:n\n",
    "    # h' = v\n",
    "    # Rectangular integration\n",
    "    # @addNLConstraint(mod, h[j] == h[j-1] + Δt*v[j-1])\n",
    "    # Trapezoidal integration\n",
    "    @constraint(mod,\n",
    "        h[j] == h[j-1] + 0.5*Δt*(v[j]+v[j-1]))\n",
    "\n",
    "    # v' = (T-D(h,v))/m - g(h)\n",
    "    # Rectangular integration\n",
    "    # @addNLConstraint(mod, v[j] == v[j-1] + Δt*(\n",
    "    #                 (T[j-1] - drag[j-1])/m[j-1] - grav[j-1]))\n",
    "    # Trapezoidal integration\n",
    "    @NLconstraint(mod,\n",
    "        v[j] == v[j-1] + 0.5*Δt*(\n",
    "            (T[j  ] - drag[j  ] - m[j  ]*grav[j  ])/m[j  ] +\n",
    "            (T[j-1] - drag[j-1] - m[j-1]*grav[j-1])/m[j-1] ))\n",
    "\n",
    "    # m' = -T/c\n",
    "    # Rectangular integration\n",
    "    # @addNLConstraint(mod, m[j] == m[j-1] - Δt*T[j-1]/c)\n",
    "    # Trapezoidal integration\n",
    "    @constraint(mod,\n",
    "        m[j] == m[j-1] - 0.5*Δt*(T[j] + T[j-1])/c)\n",
    "end\n",
    "\n",
    "# Provide starting solution\n",
    "for k in 0:n\n",
    "    set_start_value(h[k], 1)\n",
    "    set_start_value(v[k], (k/n)*(1 - (k/n)))\n",
    "    set_start_value(m[k], (m_f - m_0)*(k/n) + m_0)\n",
    "    set_start_value(T[k], T_max/2)\n",
    "end\n",
    "\n",
    "# Objective: maximize altitude at end of time of flight\n",
    "@objective(mod, Max, h[n])\n",
    "\n",
    "# Solve for the control and state\n",
    "println(\"Solving...\")\n",
    "optimize!(mod)\n",
    "\n",
    "# Display results\n",
    "#println(\"Solver status: \", status)\n",
    "println(\"Max height: \", JuMP.value(h[n]))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.1.1",
   "language": "julia",
   "name": "julia-1.1"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.1.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
